grammar org.xtext.project.rds.MyRds with org.eclipse.xtext.common.Terminals

generate myRds "http://www.xtext.org/project/rds/MyRds"

/*
 * ECSE 439: Software Language Engineering
 * Final Project - Winter 2022
 * 
 * Group 05
 * 
 */
 

RDS : 
	(users+=User (',' users+=User)*)?
	(restaurants+=Restaurant (',' restaurants+=Restaurant)*)?	
	(orders+=Order (',' orders+=Order)*)?
	
;


User:
	'User' '{'
		'fullName' ':' fullName=STRING ','
		'password' ':' password=STRING ','
		'phone' ':' phone=PHONE ','
		'email' ':' email=STRING ','
		(roles+=UserRole (',' roles+=UserRole)*)+
	'}'
;
	
UserRole : Customer | DeliveryDriver;
	
Customer:
	'Customer'  name=ID
;

DeliveryDriver:
	'DeliveryDriver' name=ID '{'
		(isAvailable?='isAvailable'',')? 
		'carLicenceNumber' ':' carLicenceNumber=STRING
	'}'
;

Order:
	'Order' orderNumber=ID '{'
		'STATUS' ':' status=OrderStatus ','
		'DELIVER_TO' ':' deliveryAddress=STRING ','
		'CUSTOMER' ':' customer=[Customer] ','
		'DRIVER' ':' deliveryDriver=[DeliveryDriver] ','
		'ORDERED_AT' ':' orderedDatetime=DATETIME ','
		'DELIVERED_AT' ':' deliveredDatetime=DATETIME ','
		'REQUESTEDDELIVERYDATETIME' ':' requestedDeliveryDatetime=DATETIME ','
		'ITEMS' ':' (orderItems+=OrderItem (',' orderItems+=OrderItem)*)?
		(','(review+=Review)?)?
	'}'
;

Restaurant:
	'Restaurant' name=ID '{'
		'address' ':' address=STRING ','
		(menu+=Menu)
	'}'
;

Menu:
	'Menu' id=ID '{'
		'items' ':' (menuItems+=MenuItem (',' menuItems+=MenuItem)*)?
	'}'
;

MenuItem:
	 '{'
	 	'name' ':' name=ID ','
		'price' ':' price=DECIMAL ','
		'description' ':' description=STRING ','
		'category' ':' category=MenuItemCategory
	'}'
;

OrderItem:
	'{'
		'quantity' ':' quantity=INT ','
		'menuItem' ':' menuItem=[MenuItem]
	'}'
;

Review:
	'Review' id=ID '{'
		'numStars' ':' numStars=INT ','
		'description' ':' description=STRING ','
		'reviewDatetime' ':' reviewDatetime=DATETIME
	'}'
;

// Enumerations
enum OrderStatus:
	ACCEPTED = 'accepted' | PREPARATION = 'preparation' | READY_PICKUP = 'ready_pickup' | PICKED_UP = 'picked_up' | DELIVERED = 'delivered'
;

enum MenuItemCategory:
	APPETIZER = 'appetizer' | SIDE_DISH = 'side_dish' | MAIN_DISH = 'main_dish' | DESSERT = 'dessert' | DRINK = 'drink'
;

// Format types
DECIMAL: INT '.' INT;
FULLNAME: STRING STRING;
PHONE: INT '-' INT '-' INT;
EMAIL: STRING '@' STRING '.com';
DATETIME: INT '-' INT '-' INT INT ':' INT;
ADDRESS: INT STRING ',' STRING;
LICENCE: STRING INT;

